#monitor


waypoint {
    linger 0,
    dist 0.009,
    target_speed 3
}

# uncomment these to change the ports on which the system listens and sends
#port 33333
#sendport 33334

# First there's a block of variable declarations. There
# should be one of these for each key which appears in the
# data stream, except for "time".

# These are surrounded by {}.

var {
    # Currently only float values are supported - we can emulate
    # others with these.
    
    # "a" is a variable with range -1 to 1. We'll store 100 values
    # for it in a cyclic buffer, so we can graph 100 values back.
    
    float a 100 range -1 to 1

    # Similarly, "b" is a 100-long buffer which takes values from -1
    # to 1.

    float b 100 range -1 to 1
    
    # 'i' is an index sent with each datum
    float i 100 range 0 to 10000000
    
    # this is a variable which reports back on "rate", which we
    # can send with the "send" button
    float rate 100 range 0 to 1
    
    
    # "lat" and "lon" should always be considered to be a pair,
    # so we link them - if a lat ever arrives without a lon, a
    # lon will be created at the same time with the same value
    # it had previously.
    # Lat has range -90 to 90, lon has range -180 to 180
    # Being linked, they have the same buffer size - 1000 values.
    
    linked (
        float lat range -90 to 90,
        float lon range -180 to 180) 1000
    linked (
        float lat2 range -90 to 90,
        float lon2 range -180 to 180) 1000
        
    }
    
    audio expr "a>0" range auto sample "warning.wav"
    
    
# That's the variables, now we can define the windows, with their
# frames and widgets.

# set up a window which is 800x800 in size (instead of size 800,800
# we could also specify "fullscreen", and we could also add
# "screen 800,600" which would move the window to a display of that size.

window size 800,800  {
    # this frame is at 0,0 (top left) and is 1x1 (the default size)
    
    frame 0,0 {

        # at top left in this frame, a gauge for variable "a". The
        # range is obtained from the variable.
        gauge 0,0 { var a }

        # below it, a gauge for variable b.
        gauge 0,1 { var b }

        # and below that, a gauge for the expression "a+b". When
        # we use an expression as a data source we must specify
        # a range.
        gauge 0,2 { expr "a+b" range -2 to 2}

        # to the right of those, and taking up 2 notional vertical
        # slots, a status block. These consist of indicators which
        # show different values depending on their sources.
    
        status 1,0,  1,2 {
            size 1,3 # there at 1x3 indicators in the block

            # floatrange is the most complex type, so I'll get it out
            # of the way - this shows different colours by assigning
            # a colour to a range of the values.
            colour puce "#400" true
            
            floatrange {
                pos 0,0
                title "a is big"
                var a   # our source

                # "bands" precedes a list of less-than conditions. The first
                # to be true is the colour we use, otherwise we use the "else"
                # colour (which has to be last)
                bands 
                <0.7    puce   # -1 to 0.7
                <0.9    yellow  # 0.7 to 0.9
                else    red     # 0.9 to 1
            }
        
            # this is very similar but uses "bands previous" to copy the
            # bands definition from the previously parsed indicator,
            # as a useful shorthand.
        
            floatrange {
                pos 0,1
                title "b is big"
                var b
                bands previous
            }
            
            # bool requires a boolean expression source,
            # which is actually a float in the range -1 to 1 (although
            # you can specify "range auto".)  
        
            bool {
                pos 0,2
                title "a and b both -ve"
                expr "a<0 && b<0" range auto
            }
        }
    
        # and finally we'll add a compass, showing a*180 (i.e. from -180 to 180)
        # as a heading.
    
        compass 1,2 {
            title "heading"
            expr "a*180" range auto
        }        
        frame 1,3,1,1 borderless {
            momentary 0,0 {
                title "quit"
                special "quit"
                key "q"
            }
            momentary 0,1 {
                title "reset"
                special "resetmaps"
                key "r"
            }
            momentary 1,0 {
                title "rate"
                out rate var rate
                key "s"
            }
        }
    }

    # now we'll add another frame to the right of that, containing
    # a map.
    
    frame 1,0 {
        map 0,0 {
            # the initial centre of the map can be specified, as can the initial
            # camera height in km
            centre 52.4,-4 height 10
            # this is a coordinate pair we send when we click on 'place waypoint' in the popup
            out wplat,wplon immediate
            point {
                # if this point renders and the map has not been autocentered,
                # do so.
                centre
                # For this point renderer,
                # we render points at lat,lon, with a new point
                # every time we get a new lat. Note that lat comes first.

                location var lat,var lon
                on var lat
            
                # we also render 40 of the previous points when new "lat" data arrived.
            
                trail 40

                # var a's range is mapped onto the entire hue range, in which 0 is red
                # and 1 is back to red again; so a=-1 will be red as will a=1. If you
                # wanted to do something cleverer, you could use an expression.
            
                hue var a
            
                # we map variable b's range (-1 to 1) onto the size range 2-20 pixels
                # to determine the size of the circle drawn.
                
                sizerange 2 20 var b
                
                # and we attach a label to each point. Note the leading
                # spaces, just to separate the point from its label a bit.
                # The %.0f is to render the float as an integer.
                
                label "   %.0f" var i
            }
            vector {
                # we also draw a vector at each point - the same points.            
                location var lat, var lon on var lat
            
                trail 40
            
                # the width of the vector is set to 3 pixels
                width 3
            
                # the direction of the vector comes from the expression a*180.
                degrees expr "a*180" range auto
            
                # and the vector is black (default is white, for technical reasons)
                colour black
                length 30
                arrow length 6 angle 30
            }
            
            line {
                # and a line between the most recent coordinate and a fixed point
                # (in the real app we'd probably use current position and current waypoint)
                
                start var lat, var lon 
                end var lat2, var lon2
                
                on var lat
                colour red
                width 3
                arrow length 30 angle 20
            }
            
            point { location 
                expr "(a-a)+51.12" range -1000 to 1000,
                expr "(a-a)-0.12" range -1000 to 1000 on var a
                colour blue
                label "  waypoint %.0f" var i
            }
            # waypoint renderer
            waypoint {}

            # now a map image
            image {
                "examplemapimage.png" # this is the bitmap
                alpha 0.6  # this line is optional, for a translucent image
                # there follow two points in the image, with coordinates
                # in lat/long and in pixels (from top left)
                
                pos 52.40163,-4.08237   image 187,411
                pos 52.44202,-4.02809   image 500,23
            }   
            image {
                "aberharbouradmir.png"
                alpha 1
                pos 52.40163,-4.08237   image 736,684
                pos 52.41678,-4.08626	image 648,114
            }
        }
    }
    
    # finally, we put a 2 wide frame in below both the others 

    frame 0,1,2,1 {
        # we fill it with a graph of a and b, 100 seconds wide.
        graph 0,0 {
            time 100
            var a { col red width 2}
            var b { col yellow width 2}
        }
    }
}
